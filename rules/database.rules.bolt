path / {
  /admins {
    /{adminId} is Admin {
      read() {isAdmin()}
      write() {isRightAdmin(key())}
    }
  }
  
  /galleries {
    /{galleries}/{gallery} is Picture {}
    read() {true}
    write() {isAdmin()}
  }
  /feedbacks {
    /{roomId} is Object {}
    read() {true}
  }
  /paragraphs {
    /{paragraphs}/{paragraph} is Paragraph {}
    read() {true}
    write() {isAdmin()}
  }
  /reservationDates {
    /{year}/{month}/{day}/{room}/{reservationId} is Reservation {}
    read() {isAdmin()}
  }
  /roomServices {
    /{roomServiceId} is RoomService {}
    read() {true}
  }
  /rooms {
    /{roomId} is Room {}
    read() {true}
    write() {isAdmin()}
  }
}

// Admin related
type Admin {
  name: String
  src: String
  lastSignInTime: String | Null
}

isAdmin() {
  return auth.uid != null && root.admins[auth.uid] != null
}

isRightAdmin(uid) {
  return isAdmin() && auth.uid === uid
}


// Gallery related
type Picture {
  title: String | Null
  desc: String | Null
  SIZE_640: String | Null
  SIZE_1024: String | Null
  SIZE_1440: String | Null
  SIZE_ORIGINAL: String | Null
  fileName: String | Null
  order: Number | Null
}


// Paragraph related
type Paragraph {
  text: String
  order: Number
}


// Reservation related
type Reservation {
  from: Boolean
  to: Boolean
}


// Room service related
type RoomService {
  name: String
  icon: String
  inRoom: Object
  write() {isAdmin()}
}


// Room related
type Room {
  unavailable: String | Null
  description: String
  id: Number
  prices: Prices
}

type Prices {
  metadata: PriceMetadata
  // REVIEW: Define better
  table: Object
  write() {isAdmin()}
}

type PriceMetadata {
  maxPeople: Number
  minPrice: Number
  write() {isAdmin()}
}